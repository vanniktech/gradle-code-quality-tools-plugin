buildscript {
    repositories {
        jcenter()

        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.gradle.plugin-publish'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:1.5.0'
}

sourceCompatibility = JavaVersion.VERSION_1_7

group = GROUP
version = VERSION_NAME

pluginBundle {
    website = 'https://github.com/vanniktech/gradle-code-quality-tools-plugin'
    vcsUrl = 'https://github.com/vanniktech/gradle-code-quality-tools-plugin'

    plugins {
        codeQualityTools {
            id = 'com.vanniktech.code.quality.tools'
            displayName = POM_NAME
            tags = ['gradle', 'android', 'java', 'findbugs', 'pmd', 'checkstyle', 'errorprone', 'multiprojects', 'app module', 'library module']
            description = POM_DESCRIPTION
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from 'build/docs/groovydoc'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives groovydocJar
    archives javadocJar
    archives sourcesJar
}

apply from: file('gradle/gradle-mvn-push.gradle')