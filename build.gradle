buildscript {
  ext.kotlinVersion = '1.3.41'
  ext.androidGradlePluginVersion = '3.6.0'

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath 'com.gradle.publish:plugin-publish-plugin:0.11.0'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.22.0'
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.19.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.15.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'
  }
}

apply plugin: 'java-library'
apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'
apply plugin: "com.vanniktech.maven.publish"
apply plugin: 'com.gradle.plugin-publish'

codeQualityTools {
  ktlint {
    toolVersion = '0.32.0'
  }
  detekt {
    toolVersion = '1.0.0'
  }
  pmd {
    enabled = false
  }
  checkstyle {
    enabled = false
  }
  cpd {
    enabled = false
  }
  errorProne {
    enabled = false
  }
}

gradlePlugin {
  plugins {
    codeQualityToolsPlugin {
      id = 'com.vanniktech.code.quality.tools'
      implementationClass = 'com.vanniktech.code.quality.tools.CodeQualityToolsPlugin'
    }
  }
}

repositories {
  mavenCentral()
  google()
  gradlePluginPortal()
}

configurations {
  fixtureClasspath
}

// Append any extra dependencies to the test fixtures via a custom configuration classpath. This
// allows us to apply additional plugins in a fixture while still leveraging dependency resolution
// and de-duplication semantics.
tasks.getByName('pluginUnderTestMetadata').getPluginClasspath().from(configurations.fixtureClasspath)

dependencies {
  api gradleApi()
  api 'de.aaschmid:gradle-cpd-plugin:3.1'

  compileOnly "com.android.tools.build:gradle:$androidGradlePluginVersion"
  compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

  testImplementation "com.android.tools.build:gradle:$androidGradlePluginVersion"
  testImplementation 'junit:junit:4.13'
  testImplementation "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

  fixtureClasspath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  fixtureClasspath "com.android.tools.build:gradle:$androidGradlePluginVersion"
}

sourceCompatibility = JavaVersion.VERSION_1_7

pluginBundle {
  website = POM_URL
  vcsUrl = POM_SCM_URL

  plugins {
    codeQualityToolsPlugin {
      displayName = POM_NAME
      tags = ['gradle', 'android', 'java', 'pmd', 'cpd', 'detekt', 'kotlin', 'ktlint', 'checkstyle', 'multiprojects', 'app module', 'library module', 'lint']
      description = POM_DESCRIPTION
    }
  }
}

wrapper {
  gradleVersion = '6.3'
  distributionType = Wrapper.DistributionType.ALL
}
